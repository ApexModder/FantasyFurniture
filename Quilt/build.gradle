plugins {
	id 'maven-publish'

	id 'com.github.johnrengelman.shadow' version '7.1.2'
}

evaluationDependsOn(':Common')

group = "${APEX_GROUP}.${MOD_ID}.quilt"
version = "${MOD_VERSION}+${MINECRAFT_VERSION}"
archivesBaseName = "${MOD_ID}-quilt"

configurations {
	common
	shadowCommon
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentQuilt.extendsFrom common
}

architectury {
	platformSetupLoomIde()
	loader('quilt')
}

loom {
	runs {
		clientAuthed {
			inherit(client)
		}
	}

	settingsPostEdit.add(new java.util.function.Consumer<net.fabricmc.loom.configuration.ide.RunConfig>() {
		@Override
		void accept(net.fabricmc.loom.configuration.ide.RunConfig runConfig) {
			if(runConfig.configName.toLowerCase(Locale.ROOT).contains('authed')) {
				runConfig.programArgs.add('--launch_target')
				runConfig.programArgs.add(runConfig.mainClass)
				runConfig.mainClass = 'net.covers1624.devlogin.DevLogin'
			}
		}
	})
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "file:///${rootProject.projectDir}/maven" }
	maven { url 'https://maven.parchmentmc.org' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://dvs1.progwml6.com/files/maven/' }
	maven { url 'https://modmaven.dev/' }
	maven { url 'https://maven.covers1624.net/' }
	maven { url 'https://maven.shedaniel.me' }
	maven { url 'https://maven.terraformersmc.com/' }
	maven { url "https://maven.quiltmc.org/repository/release/" }

	maven {
		url 'https://www.cursemaven.com'
		content { includeGroup 'curse.maven' }
	}

	maven {
		url 'https://api.modrinth.com/maven'
		content { includeGroup 'maven.modrinth' }
	}
}

dependencies {
	// Main
	runtimeClasspath "net.covers1624:DevLogin:${DEVLOGIN_VERSION}"
	modImplementation "org.quiltmc:quilt-loader:${QUILT_LOADER_VERSION}"
	modImplementation "org.quiltmc:qsl:${QUILT_LIBRARY_VERSION}+${MINECRAFT_VERSION}"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${QUILT_FABRIC_VERSION}-${MINECRAFT_VERSION}" // Try not to use fabric classes
	modImplementation("dev.architectury:architectury-fabric:${ARCHITECTURY_VERSION}") {
		exclude group: 'net.fabricmc'
		exclude group: 'net.fabricmc.fabric-api'
	}
	common(project(path: ':Common', configuration: 'namedElements')) { transitive false }
	shadowCommon(project(path: ':Common', configuration: 'transformProductionQuilt')) { transitive false }
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	modImplementation("${APEX_GROUP}.${APEXCORE_ID}.quilt:${APEXCORE_ID}-quilt:${APEXCORE_VERSION}+${MINECRAFT_VERSION}") { transitive false }

	def recipeLookupIncludeType = rootProject.hasProperty('RECIPE_LOOKUP_INCLUDE') ? rootProject.property('RECIPE_LOOKUP_INCLUDE') : ''

	// JEI
	if(rootProject.hasProperty('JEI_VERSION')) {
		modCompileOnlyApi "mezz.jei:jei-${MINECRAFT_VERSION}-fabric-api:${JEI_VERSION}"

		if(recipeLookupIncludeType == 'jei') {
			modRuntimeOnly "mezz.jei:jei-${MINECRAFT_VERSION}-fabric:${JEI_VERSION}"
		}
	}

	// REI
	if(rootProject.hasProperty('REI_VERSION')) {
		modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${REI_VERSION}") {
			exclude group: 'net.fabricmc'
			exclude group: 'net.fabricmc.fabric-api'
		}

		modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${REI_VERSION}") {
			exclude group: 'net.fabricmc'
			exclude group: 'net.fabricmc.fabric-api'
		}

		if(recipeLookupIncludeType == 'rei') {
			modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${REI_VERSION}") {
				exclude group: 'net.fabricmc'
				exclude group: 'net.fabricmc.fabric-api'
			}
		}
	}

	if(rootProject.hasProperty('EMI_VERSION')) {
		modCompileOnly("dev.emi:emi:${EMI_VERSION}+${MINECRAFT_VERSION}") {
			exclude group: 'net.fabricmc'
			exclude group: 'net.fabricmc.fabric-api'
		}

		if(recipeLookupIncludeType == 'emi') {
			modRuntimeOnly("dev.emi:emi:${EMI_VERSION}+${MINECRAFT_VERSION}") {
				exclude group: 'net.fabricmc'
				exclude group: 'net.fabricmc.fabric-api'
			}
		}
	}

	// Jade
	if(rootProject.hasProperty('JADE_CF_ID') && rootProject.hasProperty('FABRIC_JADE_CF_FILE_ID')) {
		modImplementation "curse.maven:jade-${JADE_CF_ID}:${FABRIC_JADE_CF_FILE_ID}"
	}

	// Lazy DFU
	if(rootProject.hasProperty('FABRIC_LAZY_DFU_VERSION')) {
		modRuntimeOnly "maven.modrinth:lazydfu:${FABRIC_LAZY_DFU_VERSION}"
	}

	// ModMenu
	if(rootProject.hasProperty('FABRIC_MOD_MENU_VERSION')) {
		modImplementation("com.terraformersmc:modmenu:${FABRIC_MOD_MENU_VERSION}") {
			exclude group: 'net.fabricmc'
			exclude group: 'net.fabricmc.fabric-api'
		}
	}
}

shadowJar {
	configurations = [ project.configurations.shadowCommon ]
	classifier 'dev-shadow'
}

remapJar {
	injectAccessWidener = true
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	classifier null
}

processResources {
	project.properties.each { key, value ->
		inputs.property "${key}", "${value}"
	}

	filesMatching([ '*.mixins.json', '**/*.mcmeta', 'quilt.mod.json', '**/models/**/*.json' ]) {
		expand project.properties
	}

	from project(':Common').sourceSets.main.resources
}

jar {
	classifier 'dev'
}

sourcesJar {
	def commonSources = project(':Common').sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

publishing {
	publications {
		mavenQuilt(MavenPublication) {
			groupId = "${project.group}"
			artifactId = "${project.archivesBaseName}"
			version = "${project.version}"

			from components.java
		}
	}
	repositories {
		maven { url "file:///${rootProject.projectDir}/maven" }

		if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
}
