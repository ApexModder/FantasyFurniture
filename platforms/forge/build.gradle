apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

jarJar.enable()

minecraft {
    if(rootProject.hasProperty('PARCHMENT_MAPPINGS')) {
        if(rootProject.hasProperty('PARCHMENT_DEV_MINECRAFT_VERSION')) {
            mappings channel: 'parchment', version: "${PARCHMENT_DEV_MINECRAFT_VERSION}-${PARCHMENT_MAPPINGS}-${MINECRAFT_VERSION}"
        } else {
            mappings channel: 'parchment', version: "${PARCHMENT_MAPPINGS}-${MINECRAFT_VERSION}"
        }
    } else {
        mappings channel: 'official', version: "${MINECRAFT_VERSION}"
    }

    if(rootProject.file("${AT_FILE}").exists()) {
        accessTransformer = rootProject.file("${AT_FILE}")
    }

    runs {
        client {
            workingDirectory file('run/client')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property "${MOD_ID}.test_elements.enabled", 'true'
            ideaModule "${rootProject.name}.${project.name}.main"
            jvmArg '-XX:+AllowEnhancedClassRedefinition' // requires JBR
            folderName 'forge'

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }

        if(rootProject.hasProperty('DEVLOGIN_VERSION')) {
            clientLogin {
                parent runs.client
                main 'net.covers1624.devlogin.DevLogin'
                args '--launch_target', '{LAUNCH_TARGET}'
                lazyToken("LAUNCH_TARGET", { runs.client.main })
            }
        }

        server {
            workingDirectory file('run/server')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property "${MOD_ID}.test_elements.enabled", 'true'
            ideaModule "${rootProject.name}.${project.name}.main"
            args 'nogui'
            jvmArg '-XX:+AllowEnhancedClassRedefinition' // requires JBR
            folderName 'forge'

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('run/client')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            ideaModule "${rootProject.name}.${project.name}.main"
            folderName 'forge'

            args '--mod', MOD_ID, '--client', '--server', '--validate'
            args '--output', file('src/generated/resources')
            args '--existing', project(':common').file('src/main/resources/')
            args '--existing', file('src/main/resources/')

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${MOD_ID}.refmap.json"

    if(file("src/main/resources/${MOD_ID}.mixins.json").exists()) {
        config "${MOD_ID}.mixins.json"
    }

    if(project(':common').file("src/main/resources/${MOD_ID}-common.mixins.json").exists()) {
        config "${MOD_ID}-common.mixins.json"
    }
}

dependencies {
    if(rootProject.hasProperty('FORGE_DEV_VERSION')) {
        minecraft "net.minecraftforge:forge:${MINECRAFT_VERSION}-${FORGE_DEV_VERSION}"
    } else {
        minecraft "net.minecraftforge:forge:${MINECRAFT_VERSION}-${FORGE_VERSION}"
    }

    annotationProcessor "org.spongepowered:mixin:${MIXIN_VERSION}:processor"

    compileOnly project(path: ':common', configuration: 'namedElements')

    if(rootProject.hasProperty('APEXCORE_DEV_VERSION')) {
        implementation fg.deobf("xyz.apex.minecraft.apexcore:apexcore-forge:${APEXCORE_DEV_VERSION}+${MINECRAFT_VERSION}")
    } else {
        implementation fg.deobf("xyz.apex.minecraft.apexcore:apexcore-forge:${APEXCORE_VERSION}+${MINECRAFT_VERSION}")
    }

    if(rootProject.hasProperty('DEVLOGIN_VERSION')) {
        runtimeOnly "net.covers1624:DevLogin:${DEVLOGIN_VERSION}"
    }
}

processResources {
    from project(':common').sourceSets.main.resources
}

compileJava {
    source project(':common').sourceSets.main.allSource
}

sourcesJar {
    from project(':common').sourceSets.main.allJava
}

tasks.jarJar.configure {
    archiveClassifier.set('')
}

jar {
    archiveClassifier.set('slim')
}

artifacts {
    archives tasks.jarJar
}

reobf {
    jarJar { }
}

jar.finalizedBy('reobfJar')
tasks.jarJar.finalizedBy('reobfJarJar')

collectJars {
    def tasks = [ jar, sourcesJar, tasks.jarJar ]
    dependsOn tasks
    from tasks
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact tasks.jarJar
        }
    }
}
