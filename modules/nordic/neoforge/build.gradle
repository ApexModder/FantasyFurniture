apply from: rootProject.file('gradle/scripts/neoforge.gradle')

minecraft {
    runs {
        configureEach {
            mods {
                "${MOD_ID}_nordic" {
                    source project(':nordic-common').sourceSets.main
                    source sourceSets.main
                }

                "${MOD_ID}" {
                    source project(':common').sourceSets.main
                    source project(':neoforge').sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('run/client')

            args '--mod', "${MOD_ID}_nordic", '--client', '--server', '--validate'
            args '--output', file('src/generated/resources')
            args '--existing', project(':common').file('src/main/resources/')
            args '--existing', project(':nordic-common').file('src/main/resources/')
            args '--existing', project(':neoforge').file('src/main/resources/')
            args '--existing', file('src/main/resources/')
            args '--existing-mod', "${APEXCORE_ID}"
            args '--existing-mod', "${MOD_ID}"
        }
    }
}

dependencies {
    // required as forge jarJar is including the 'slim'
    // jar, and i dont see any way to redirect it to
    // include the full fat jarJar
    //
    // this should be included via the FF jar
    jarJar("xyz.apex.minecraft.apexcore:apexcore-neoforge:${APEXCORE_VERSION}+${MINECRAFT_VERSION}") {
        transitive false
        jarJar.ranged(it, "${APEXCORE_VERSION_RANGE}")
    }

    compileOnly project(path: ':nordic-common', configuration: 'namedElements')
    implementation project(':neoforge')

    jarJar(project(':neoforge')) {
        transitive false
        jarJar.ranged(it, "[${MOD_VERSION},)")
    }
}

processResources {
    from project(':nordic-common').sourceSets.main.resources
}

compileJava {
    source project(':nordic-common').sourceSets.main.allSource
}

sourcesJar {
    from project(':nordic-common').sourceSets.main.allJava
}