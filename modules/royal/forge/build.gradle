apply from: rootProject.file('modules/forge.gradle')

minecraft {
    runs {
        data {
            workingDirectory file('run/client')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            ideaModule "${rootProject.name}.${project.name}.main"
            forceExit false

            args '--mod', "${MOD_ID}_royal", '--client', '--server', '--validate'
            args '--input', project(':royal-common').file('blockbench/')
            args '--output', project(':royal-common').file('src/main/generated/')
            args '--existing', project(':base-common').file('src/main/resources')
            args '--existing', project(':base-forge').file('src/main/resources')
            args '--existing', project(':royal-common').file('src/main/resources')
            args '--existing', file('src/main/resources/')
            args '--existing-mod', 'apexcore'
            args '--existing-mod', MOD_ID

            mods {
                "${MOD_ID}" {
                    source project(':base-common').sourceSets.main
                    source project(':base-forge').sourceSets.main
                }

                "${MOD_ID}_royal" {
                    source project(':royal-common').sourceSets.main
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    compileOnly project(path: ':base-common', configuration: 'namedElements')
    compileOnly project(':base-forge')

    compileOnly project(path: ':royal-common', configuration: 'namedElements')

    jarJar("xyz.apex.minecraft.apexcore:apexcore-forge:${APEXCORE_FORGE_VERSION_RANGE}")

    jarJar(project(':base-forge')) {
        transitive false
        jarJar.ranged(it, "[${MOD_VERSION},)")
    }
}

processResources {
    from project(':royal-common').sourceSets.main.resources
}

compileJava {
    source project(':royal-common').sourceSets.main.allSource
}

sourcesJar {
    from project(':royal-common').sourceSets.main.allJava
}
