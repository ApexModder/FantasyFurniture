plugins {
	id 'maven-publish'
}

group = "${APEX_GROUP}.${MOD_ID}.shared"
version = "${MOD_VERSION}+${MINECRAFT_VERSION}"
archivesBaseName = "${MOD_ID}-shared"

sourceSets {
	main {
		resources.srcDirs += 'src/main/generated'
	}
}

architectury {
	common(['forge', 'fabric', 'quilt'])
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "file:///${rootProject.projectDir}/maven" }
	maven { url 'https://maven.parchmentmc.org' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://dvs1.progwml6.com/files/maven/' }
	maven { url 'https://modmaven.dev/' }
	maven { url 'https://maven.covers1624.net/' }
	maven { url 'https://maven.shedaniel.me' }

	maven {
		url 'https://www.cursemaven.com'
		content { includeGroup 'curse.maven' }
	}

	maven {
		url 'https://api.modrinth.com/maven'
		content { includeGroup 'maven.modrinth' }
	}
}

dependencies {
	compileOnly "org.spongepowered:mixin:${MIXIN_VERSION}"
	modApi "dev.architectury:architectury:${ARCHITECTURY_VERSION}"
	implementation 'com.google.code.findbugs:jsr305:3.0.2'
	implementation 'com.google.errorprone:error_prone_annotations:2.11.0'
	modImplementation("${APEX_GROUP}.${APEXCORE_ID}.shared:${APEXCORE_ID}-shared:${APEXCORE_VERSION}+${MINECRAFT_VERSION}") { transitive false }

	// JEI
	if(rootProject.hasProperty('JEI_VERSION')) {
		modCompileOnlyApi("mezz.jei:jei-${MINECRAFT_VERSION}-common-api:${JEI_VERSION}")
	}

	// REI
	if(rootProject.hasProperty('REI_VERSION')) {
		modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${REI_VERSION}"
		modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin:${REI_VERSION}"
	}

	if(rootProject.hasProperty('CLOTH_CONFIG_VERSION')) {
		modApi "me.shedaniel.cloth:cloth-config:${CLOTH_CONFIG_VERSION}"
	}
}

processResources {
	project.properties.each { key, value ->
		inputs.property "${key}", "${value}"
	}

	filesMatching([ '*.mixins.json', '**/*.mcmeta', '**/models/**/*.json' ]) {
		expand project.properties
	}
}

publishing {
	publications {
		mavenCommon(MavenPublication) {
			groupId = "${project.group}"
			artifactId = "${project.archivesBaseName}"
			version = "${project.version}"

			from components.java
		}
	}
	repositories {
		maven { url "file:///${rootProject.projectDir}/maven" }

		if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
}
