plugins {
    id 'net.minecraftforge.gradle' version '5.1.+' apply false
    id 'org.parchmentmc.librarian.forgegradle' version '1.+' apply false
    id 'org.spongepowered.mixin' version '0.7.+' apply false
    id 'fabric-loom' version '1.1-SNAPSHOT' apply false

    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    project.ext {
        accessWidenerFile = rootProject.file("modules/base/fabric/src/main/resources/${MOD_ID}.accesswidener")
        accessTransformerFile = rootProject.file('modules/base/forge/src/main/resources/META-INF/accesstransformer.cfg')
    }

    group = "${MOD_GROUP}"
    version = "${MOD_VERSION}+${MINECRAFT_VERSION}"
    archivesBaseName = project.name.startsWithIgnoreCase('workspace') ? "${MOD_ID}-${project.name.substring(project.name.indexOf('-') + 1)}" : "${MOD_ID}-${project.name}"

    println("${project.path} -> '${project.group}:${project.archivesBaseName}:${project.version}'")

    sourceSets {
        main.resources.srcDirs += [ 'src/main/generated' ]

        test {
            java.srcDirs = [ ]
            resources.srcDirs = [ ]
        }
    }

    repositories {
        mavenLocal()
        maven { url 'https://maven.parchmentmc.org' }
        maven { url 'https://maven.covers1624.net/' }
        maven { url 'https://maven.terraformersmc.com/releases/' }

        maven {
            url 'https://www.cursemaven.com'
            content { includeGroup 'curse.maven' }
        }

        maven {
            url 'https://api.modrinth.com/maven'
            content { includeGroup 'maven.modrinth' }
        }
    }

    dependencies {
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        compileOnly 'com.google.errorprone:error_prone_annotations:2.11.0'
    }

    processResources {
        project.properties.each {
            inputs.property "${it.key}", "${it.value}"
        }

        filesMatching([ '*.mixins.json', 'pack.mcmeta', 'META-INF/mods.toml', 'fabric.mod.json', 'architectury.common.json' ]) {
            expand project.properties
        }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of("${JAVA_VERSION}"))
        }

        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(JavaLanguageVersion.of("${JAVA_VERSION}").asInt())

        javaToolchains {
            compilerFor {
                languageVersion.set(JavaLanguageVersion.of("${JAVA_VERSION}"))
            }
        }
    }

    jar {
        manifest {
            attributes([
                    'Specification-Title': "${MOD_ID}",
                    'Specification-Vendor': 'ApexStudios',
                    'Specification-Version': "${MINECRAFT_VERSION}",
                    'Implementation-Title': "${project.name}",
                    'Implementation-Version': "${project.version}",
                    'Implementation-Vendor': 'ApexStudios',
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = "${project.group}"
                artifactId = "${project.archivesBaseName}"
                version = "${project.version}"
            }
        }

        repositories {
            if(System.getenv('MAVEN_PASSWORD') != null) {
                maven {
                    name 'Covers1624-Maven'
                    url 'https://nexus.covers1624.net/repository/apex-releases/'

                    credentials {
                        username 'apex'
                        password System.getenv('MAVEN_PASSWORD')
                    }
                }
            }
        }
    }

    idea.module {
        excludeDirs += [
                file('.gradle'),
                file('build'),
                file('run')
        ]
    }
}

tasks.register('collectJars', Copy) {
    def deps = subprojects.collect {
        if(it.name.containsIgnoreCase('common') || it.name.containsIgnoreCase('forge')) {
            return it.tasks.withType(Jar)
        } else {
            return [ it.tasks.remapJar, it.tasks.sourcesJar ]
        }
    }

    dependsOn deps
    from deps
    into 'jars'
}

idea.module {
    excludeDirs += [
            file('.idea'),
            file('.gradle'),
            file('gradle')
    ]
}
