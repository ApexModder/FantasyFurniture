pluginManagement {
    repositories {
        exclusiveContent {
            forRepository { maven { url 'https://maven.minecraftforge.net' } }
            filter { includeGroupByRegex 'net\\.minecraftforge.*' }
        }

        exclusiveContent {
            forRepository { maven { url 'https://maven.fabricmc.net' } }
            filter {
                includeGroup 'net.fabricmc'
                includeGroup 'fabric-loom'
            }
        }

        exclusiveContent {
            forRepository { maven { url 'https://maven.parchmentmc.org' } }
            filter { includeGroupByRegex 'org\\.parchmentmc.*' }
        }

        exclusiveContent {
            forRepository { maven { url 'https://repo.spongepowered.org/repository/maven-public' } }
            filter { includeGroupByRegex 'org\\.spongepowered.*' }
        }

        gradlePluginPortal()
    }

    resolutionStrategy {
        eachPlugin {
            if(requested.id.id == "org.spongepowered.mixin") {
                useModule("org.spongepowered:mixingradle:${requested.version}")
            }
        }
    }
}

[
        "common",
        "forge",
        "fabric",
].forEach { platform ->
    include(platform)
    project(":${platform}").projectDir = file("platforms/${platform}")
}

rootProject.name = "${MOD_NAME}-${MINECRAFT_VERSION}"