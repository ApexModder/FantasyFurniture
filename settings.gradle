pluginManagement {
	repositories {
		maven {
			url 'https://maven.neoforged.net'
			content {
				includeGroupByRegex 'net\\.neoforged.*'
			}
		}

		maven {
			url 'https://maven.minecraftforge.net'
			content {
				includeGroupByRegex 'net\\.minecraftforge.*'
			}
		}

		maven {
			url 'https://maven.fabricmc.net'
			content {
				includeGroup 'net.fabricmc'
				includeGroup 'fabric-loom'
			}
		}

		maven {
			url 'https://maven.parchmentmc.org'
			content {
				includeGroupByRegex 'org\\.parchmentmc.*'
			}
		}

		maven {
			url 'https://repo.spongepowered.org/repository/maven-public'
			content {
				includeGroupByRegex 'org\\.spongepowered.*'
			}
		}

		gradlePluginPortal()
	}

	resolutionStrategy {
		eachPlugin {
			if(requested.id.id == "org.spongepowered.mixin") {
				useModule("org.spongepowered:mixingradle:${requested.version}")
			}
		}
	}
}

plugins {
	id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

def includeExt = { name, dir ->
	include name
	project(":${name}").projectDir = file "projects/${dir}"
}

def includePlatform = { platform ->
	includeExt platform, "platforms/${platform}"
	includeExt "complete-${platform}", "complete/${platform}"

	includeExt "nordic-${platform}", "modules/nordic/${platform}"
}

includePlatform 'common'
// includePlatform 'neoforge'
includePlatform 'mcforge'
includePlatform 'fabric'

rootProject.name = "${MOD_NAME}"