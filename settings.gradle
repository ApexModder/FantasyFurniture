pluginManagement {
    repositories {
        exclusiveContent {
            forRepository { maven { url 'https://maven.neoforged.net' } }
            filter { includeGroupByRegex 'net\\.neoforged.*' }
            // TODO: Why is this still needed? we are running NeoForge not MinecraftForge
            filter { includeGroupByRegex 'net\\.minecraftforge.*' }
        }

        exclusiveContent {
            forRepository { maven { url 'https://maven.fabricmc.net' } }
            filter {
                includeGroup 'net.fabricmc'
                includeGroup 'fabric-loom'
            }
        }

        exclusiveContent {
            forRepository { maven { url 'https://maven.parchmentmc.org' } }
            filter { includeGroupByRegex 'org\\.parchmentmc.*' }
        }

        exclusiveContent {
            forRepository { maven { url 'https://repo.spongepowered.org/repository/maven-public' } }
            filter { includeGroupByRegex 'org\\.spongepowered.*' }
        }

        gradlePluginPortal()
    }

    resolutionStrategy {
        eachPlugin {
            if(requested.id.id == "org.spongepowered.mixin") {
                useModule("org.spongepowered:mixingradle:${requested.version}")
            }
        }
    }
}

def platforms = [
        'common',
        'neoforge',
        'fabric'
]

def modules = [
        'nordic'
]

def includeExt = { name, dir ->
    include name
    project(":${name}").projectDir = file(dir)
}

platforms.forEach { platform ->
    includeExt platform, "platforms/${platform}"
    includeExt "workspace-${platform}", "workspace/${platform}"

    modules.forEach { module ->
        includeExt "${module}-${platform}", "modules/${module}/${platform}"
    }
}

rootProject.name = "${MOD_NAME}-${MINECRAFT_VERSION}"