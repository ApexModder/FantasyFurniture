pluginManagement {
	repositories {
		gradlePluginPortal()
		mavenLocal()

		maven { url 'https://maven.minecraftforge.net' }
		maven { url 'https://maven.fabricmc.net' }
		maven { url 'https://maven.parchmentmc.org' }
	}

	resolutionStrategy {
		eachPlugin {
			if(requested.id.id == 'org.spongepowered.mixin') {
				useModule("org.spongepowered:mixingradle:${requested.version}")
			}
		}
	}
}

loadModule('base')
loadModule('workspace')

loadModule('nordic')
loadModule('dunmer')
loadModule('venthyr')
loadModule('bone')
loadModule('royal')
loadModule('necrolord')

void loadModule(String module) {
	loadModule(module, 'common')
	loadModule(module, 'forge')
	loadModule(module, 'fabric')
}

void loadModule(String module, String platform) {
	if(file("modules/${module}/${platform}/build.gradle").exists()) {
		include "${module}-${platform}"
		def p = project(":${module}-${platform}")
		p.projectDir = file("modules/${module}/${platform}")
	}
}

rootProject.name = "FantasyFurniture-${MINECRAFT_VERSION}"
